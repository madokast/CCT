{cosy script for HUST SC Gantry DownStream, B. Qin, 2019-12-25}

INCLUDE 'COSY' ;
PROCEDURE RUN ;

VARIABLE I 1 ; {number of iteration} VARIABLE refresh 1 ; {print picture per refresh}
VARIABLE DL1 1 ; VARIABLE DL2 1 ;
VARIABLE CCT12_APER 1 ; VARIABLE CCT345_APER 1 ; {half aperture}
VARIABLE CCT1_ANG 1 ; VARIABLE CCT2_ANG 1 ; VARIABLE CCT3_ANG 1 ;
VARIABLE CCT12_ANG_MIN 1 ; VARIABLE CCT345_ANG_MIN 1 ;  
VARIABLE CCT4_ANG 1 ; VARIABLE CCT5_ANG 1 ;{angle}
VARIABLE CCT12_n 1 ; VARIABLE CCT345_n 1 ;
VARIABLE CCT12_R 1 ; VARIABLE CCT345_R 1 ;
VARIABLE QS1_LEN 1 ; VARIABLE QS1_Q 1 ; VARIABLE QS1_S 1 ; VARIABLE QS1_APER 1 ;
VARIABLE QS2_LEN 1 ; VARIABLE QS2_Q 1 ; VARIABLE QS2_S 1 ; VARIABLE QS2_APER 1 ;
VARIABLE QS3_LEN 1 ; VARIABLE QS3_Q 1 ; VARIABLE QS3_S 1 ; VARIABLE QS3_APER 1 ;
VARIABLE GAP1 1 ; VARIABLE GAP2 1 ; VARIABLE GAP3 1 ;

VARIABLE OBJ_ANG45 1 ; VARIABLE OBJ_ANG135 1 ;
VARIABLE OBJ_R16_1 1 ; VARIABLE OBJ_R16_2 1;
VARIABLE OBJ_R26_1 1 ; VARIABLE OBJ_R26_2 1;
VARIABLE OBJ_R11_1 1 ; VARIABLE OBJ_R11_2 1;
VARIABLE OBJ_R12_1 1 ; VARIABLE OBJ_R12_2 1;
VARIABLE OBJ_R21_1 1 ; VARIABLE OBJ_R21_2 1;
VARIABLE OBJ_R33_1 1 ; VARIABLE OBJ_R33_2 1;
VARIABLE OBJ_R34_1 1 ; VARIABLE OBJ_R34_2 1;
VARIABLE OBJ_R43_1 1 ; VARIABLE OBJ_R43_2 1;
VARIABLE OBJ_T126_1 1 ; VARIABLE OBJ_T126_2 1;
VARIABLE OBJ_T226_1 1 ; VARIABLE OBJ_T226_2 1;
VARIABLE OBJ_T346_1 1 ; VARIABLE OBJ_T346_2 1;
VARIABLE OBJ_T446_1 1 ; VARIABLE OBJ_T446_2 1;
VARIABLE OBJ_SIGMA11_1 1 ; VARIABLE OBJ_SIGMA11_2 1 ;
VARIABLE OBJ_SIGMA33_1 1 ; VARIABLE OBJ_SIGMA33_2 1 ;
VARIABLE OBJ_ALIGN 1 ;

VARIABLE INPUT_FILE 1;{输入文件}
VARIABLE OUTFPUT_FILE 1;{输出文件}
VARIABLE LINE 1000;{装垃圾的}
VARIABLE INPUT_NUMBER 1;{输入组数}
VARIABLE CURRENT_ID 1; {当前组数}

FUNCTION SQUA X ; SQUA := X * X ; ENDFUNCTION ;


I := 0 ; {number of iteration}
refresh := 1 ; {print picture per refresh}

{--------------  CONSTANT 常量  --------------------}
CCT12_R := 1.00 ; {偏转半径}
CCT345_R := 1.00 ;

CCT12_APER := 0.020 ;{CCT1和CCT2的半孔径}
CCT345_APER := 0.060 ;


QS1_APER := 0.030 ;
QS2_APER := 0.030 ;{QS1、QS2的半孔径}
QS3_APER := 0.060 ;

CCT12_ANG_MIN := 5.0 ; {CCT1和CCT2的偏转角度的最小值，不要小于5度}
CCT345_ANG_MIN := 8 ;

{--------------  VARIABLE 变量 共12个  --------------------}


DL1 := 1.1759 ;  {第一偏转段的前后偏移段长度 0.5m-1.3m}
QS1_LEN := 0.2; {0.15m~0.25m}
QS2_LEN := 0.2; {0.15m~0.25m}{QS1、QS2的长度}
GAP1 := 0.15 ; {0.12m~0.2m}
GAP2 := 0.15 ; {0.12m~0.2m}{元件间距}

CCT1_ANG:= 4.116696651312341;
CCT2_ANG:= 8.383304669429050; {CCT1和CCT2的偏转角度}{ CCT1_ANG+CCT12_ANG_MIN+CCT2_ANG+CCT12_ANG_MIN 加起来必须等于22.5 }

CCT12_n:=-13.63885007227421; {CCT1和CCT2的四极场参数} {-25~25 可以使劲变化}

QS1_Q:= 1.596245173043834;
QS2_Q:=-1.133857472311528; {QS1、QS2的四极场参数} {绝对值越小越好，至少要比现在小，最好是小于0.5}

QS1_S:=0.1877291149237406;
QS2_S:=-.3158579746136076; {QS1、QS2的六极场参数} {绝对值越小越好，至少要比现在小}


DL2 := 2.40 ;
QS3_LEN := 0.2;
GAP3:=0.2585751675857585;

CCT3_ANG:=0.3926893224425800E-005;
CCT4_ANG:= 21.99845340255313;
CCT5_ANG:= 21.50155751013952;

CCT345_n:=-4.216451945875646;

QS3_Q:=0.4569;
QS3_S:=-0.7157;

{--------------PARAMETERS END--------------------}  

INPUT_FILE := 8;
OUTFPUT_FILE := 9;

OPENF INPUT_FILE 'input.txt' 'OLD' ;
OPENF OUTFPUT_FILE 'output.txt' 'REPLACE';

READ INPUT_FILE LINE; {丢弃第一行}

READ INPUT_FILE LINE; {读输入的组数}
INPUT_NUMBER := RE(LINE);



WRITE OUTFPUT_FILE 'ID OBJ_ANG45 OBJ_R16_1 OBJ_R26_1 OBJ_R12_1 OBJ_R34_1 OBJ_R11_1 OBJ_R33_1 OBJ_R21_1 OBJ_R43_1 OBJ_T126_1 OBJ_T346_1' ;
WRITE OUTFPUT_FILE 'ID OBJ_R16_2 OBJ_R26_2 OBJ_R12_2 OBJ_R34_2 OBJ_R11_2 OBJ_R33_2 OBJ_T126_2 OBJ_T346_2 OBJ_T226_2 OBJ_T446_2 OBJ_ALIGN' ;

{-------------------- START RUN ------------------}

CURRENT_ID := 0;

WRITE 6 INPUT_NUMBER;

WHILE CURRENT_ID<INPUT_NUMBER ;

READ INPUT_FILE LINE; {读当前组数 CURRENT_ID}
CURRENT_ID := RE(LINE);

WRITE 6 'COMPUTING '&ST(CURRENT_ID);


READ INPUT_FILE LINE; DL1 := RE(LINE);
READ INPUT_FILE LINE; QS1_LEN := RE(LINE);
READ INPUT_FILE LINE; QS2_LEN := RE(LINE);
READ INPUT_FILE LINE; GAP1 := RE(LINE);
READ INPUT_FILE LINE; GAP2 := RE(LINE);
READ INPUT_FILE LINE; CCT1_ANG := RE(LINE);
READ INPUT_FILE LINE; CCT2_ANG := RE(LINE);
READ INPUT_FILE LINE; CCT12_n := RE(LINE);
READ INPUT_FILE LINE; QS1_Q := RE(LINE);
READ INPUT_FILE LINE; QS2_Q := RE(LINE);
READ INPUT_FILE LINE; QS1_S := RE(LINE);
READ INPUT_FILE LINE; QS2_S := RE(LINE);
READ INPUT_FILE LINE; DL2 := RE(LINE);
READ INPUT_FILE LINE; QS3_LEN := RE(LINE);
READ INPUT_FILE LINE; GAP3 := RE(LINE);
READ INPUT_FILE LINE; CCT3_ANG := RE(LINE);
READ INPUT_FILE LINE; CCT4_ANG := RE(LINE);
READ INPUT_FILE LINE; CCT5_ANG := RE(LINE);
READ INPUT_FILE LINE; CCT345_n := RE(LINE);
READ INPUT_FILE LINE; QS3_Q := RE(LINE);
READ INPUT_FILE LINE; QS3_S := RE(LINE);



OV 2 3 0 ; {order 1, phase space MSm 3, # of parameters 0}
{ PTY 0.0 ; {picture type 0.0 for straight-line and nonzore for lab} }
RPP 250 ; {particle type = proton, kinetic energy = 250MeV}
SB 3.5E-3 7.5E-3 0    3.5E-3 7.5E-3 0    0 0 0 0 0;
{<x><x'><r12>       <y><y'><r34>     <t'><dE/E><r56><dm/m><z'>}
{x=2.0mm x'=3.0mr y=2.0mm y'=3.0mm z=0.0mm DP/p=5.0% momentum=729.134GeV/c}
CR ;

ENVEL ;

UM ;    {sets map to unity} 
{ BP ; }

{-------------------------BEAMLINE START-------------------------}
{----FIRST BENDING 第一偏转段----}
DL ABS(DL1) ;
CB ; MS CCT12_R ABS(CCT1_ANG)+CCT12_ANG_MIN CCT12_APER CCT12_n 0 0 0 0; CB ; 
CB ; MS CCT12_R ABS(CCT2_ANG)+CCT12_ANG_MIN CCT12_APER -CCT12_n 0 0 0 0; CB ; 
DL ABS(GAP1);

M5 ABS(QS1_LEN) QS1_Q QS1_S 0 0 0 QS1_APER;
DL ABS(GAP2);
M5 ABS(QS2_LEN) QS2_Q QS2_S 0 0 0 QS2_APER;
DL ABS(GAP2);
M5 ABS(QS1_LEN) QS1_Q QS1_S 0 0 0 QS1_APER;

DL ABS(GAP1);
CB ; MS CCT12_R ABS(CCT2_ANG)+CCT12_ANG_MIN CCT12_APER -CCT12_n 0 0 0 0; CB ; 
CB ; MS CCT12_R ABS(CCT1_ANG)+CCT12_ANG_MIN CCT12_APER CCT12_n 0 0 0 0; CB ; 
DL ABS(DL1) ;

{----目标 共11个  ----}
OBJ_ANG45 := (ABS(CCT1_ANG)+CCT12_ANG_MIN + ABS(CCT2_ANG))+CCT12_ANG_MIN - 22.5 ; 

OBJ_R16_1 := ABS(ME(1,6)) ;                  OBJ_R26_1 := ABS(ME(2,6)) ;
OBJ_R12_1 := ME(1,2);                        OBJ_R34_1 := ME(3,4);
OBJ_R11_1 := ABS(ME(1,1))- 1. ;              OBJ_R33_1 := ABS(ME(3,3))- 1.; 
OBJ_R21_1 := ME(2,1);                        OBJ_R43_1 := ME(4,3) ;
OBJ_T126_1 := ABS(ME(1,26)) ;                OBJ_T346_1 := ABS(ME(3,46)) ; 

{----SECOND BENDING 第二偏转段 ----}
DL ABS(DL2) ;

MS CCT345_R ABS(CCT3_ANG)+CCT345_ANG_MIN CCT345_APER CCT345_n 0 0 0 0;
MS CCT345_R ABS(CCT4_ANG)+CCT345_ANG_MIN CCT345_APER -CCT345_n 0 0 0 0;
MS CCT345_R ABS(CCT5_ANG)+CCT345_ANG_MIN CCT345_APER CCT345_n 0 0 0 0;
DL ABS(GAP3);

M5 ABS(QS3_LEN) QS3_Q QS3_S 0 0 0 QS3_APER;

DL ABS(GAP3);
MS CCT345_R ABS(CCT5_ANG)+CCT345_ANG_MIN CCT345_APER CCT345_n 0 0 0 0;
MS CCT345_R ABS(CCT4_ANG)+CCT345_ANG_MIN CCT345_APER -CCT345_n 0 0 0 0;
MS CCT345_R ABS(CCT3_ANG)+CCT345_ANG_MIN CCT345_APER CCT345_n 0 0 0 0;

DL ABS(DL2) ;

OBJ_R16_2 := ABS(ME(1,6)) ;    OBJ_R26_2 := ABS(ME(2,6)) ;
OBJ_R12_2 := ME(1,2) *1 ;      OBJ_R34_2 := ME(3,4) *1 ;
OBJ_R11_2 := ABS(ME(1,1))- 1 ; OBJ_R33_2 := ABS(ME(3,3))- 1; 
OBJ_T126_2 := ABS(ME(1,26)) ;  OBJ_T346_2 := ABS(ME(3,46)) ; 
OBJ_T226_2 := ABS(ME(2,26)) ;  OBJ_T446_2 := ABS(ME(4,46)) ; 

OBJ_ALIGN := (GAP1*2+GAP2*2+QS1_LEN*2+QS2_LEN)*SIN(22.5/180*3.14159)+(DL1+DL2)*SIN(45/180*3.14159)-(GAP3*2+QS3_LEN)*SIN(22.5/180*3.14159)-DL2-1.00*(SQRT(2)-1);
{-------------------------BEAMLINE  END-------------------------}
{WRITE OUTFPUT_FILE ST(CURRENT_ID)&' '&ST(OBJ_ANG45)&' '&ST(OBJ_R16_1)&' '&ST(OBJ_R26_1)&' '&ST(OBJ_R12_1)&' '&ST(OBJ_R34_1)&' '&ST(OBJ_R11_1)&' '&ST(OBJ_R33_1)&' '&ST(OBJ_R21_1)&' '&ST(OBJ_R43_1)&' '&ST(OBJ_T126_1)&' '&ST(OBJ_T346_1);}
WRITE OUTFPUT_FILE ST(CURRENT_ID)&' '&ST(OBJ_R16_2)&' '&ST(OBJ_R26_2)&' '&ST(OBJ_R12_2)&' '&ST(OBJ_R34_2)&' '&ST(OBJ_R11_2)&' '&ST(OBJ_R33_2)&' '&ST(OBJ_T126_2)&' '&ST(OBJ_T346_2)&' '&ST(OBJ_T226_2)&' '&ST(OBJ_T446_2)&' '&ST(OBJ_ALIGN);

ENDWHILE ;

CLOSEF INPUT_FILE;
CLOSEF OUTFPUT_FILE;
  
{ EP ; {end the picture} PG -12 -12 ; {print to screen} }

ENDPROCEDURE ;
RUN ; END ;
